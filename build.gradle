apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
group = 'com.github.masonm'
archivesBaseName = 'wiremock-snapshot'
version = '0.3a'

repositories {
    mavenCentral()
}

sourceSets {
    test {
        java {
            include 'com/github/tomakehurst/wiremock/testsupport/**'
            include 'com/github/tomakehurst/wiremock/matching/MockRequest*'
            include 'com/github/masonm/**'
        }
    }
}

task fatJar (type: Jar) {
    classifier = 'standalone'
    baseName = project.name + '-standalone'

    manifest {
        attributes("Main-Class": "com.github.masonm.wiremock.SnapshotExtension")
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }

    with jar
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar, fatJar
}

signing {
    sign configurations.archives
}

if (!hasProperty('ossrhUsername') || !hasProperty('ossrhPassword')) {
    ext.ossrhUsername = ''
    ext.ossrhPassword = ''
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'wiremock-snapshot'
                packaging 'jar'
                description 'Wiremock API extension to record stub mappings'
                url 'https://github.com/MasonM/wiremock-snapshot'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/license/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id  'masonm'
                        name 'Mason Malone'
                    }
                }

                scm {
                    connection 'scm:git:git://github.com/MasonM/wiremock-snapshot.git'
                    developerConnection 'scm:git:ssh://github.com:MasonM/wiremock-snapshot.git'
                    url 'https://github.com/MasonM/wiremock-snapshot'
                }
            }
        }
    }
}

test {
    maxParallelForks = 3

    exclude 'com/github/tomakehurst/**'

    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.6.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'org.jmock', name: 'jmock', version: '2.5.1'
    testCompile group: 'org.jmock', name: 'jmock-junit4', version: '2.5.1'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.2.3'
    testCompile group: 'com.toomuchcoding.jsonassert', name: 'jsonassert', version: '0.4.7'
}